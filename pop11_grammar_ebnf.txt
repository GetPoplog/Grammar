Grammar ::=
	Expression

Expression ::=
	NonEmptyExpression?

NonEmptyExpression ::=
	Literal
	| ConstantExpression
	| Variable
	| Assignment
	| Definition
	| Declaration
	| Conditional
	| BooleanExpression
	| Iterative
	| OperatorExpression
	| FunctionApplication
	| Jump
	| Label
	| '(' Sequence ')'

ConstantExpression ::=
	Construct
	| '"' Word '"'
	| '"' String '"'

Definition ::=
	'define' Word ('(' Varslist? ')' | Varslist)? ('->' Varslist)* ';' Sequence 'enddefine'

Assignment ::=
	NonEmptyExpression ('->' | '->>') (Variable | FunctionApplication)
	| NonEmptyExpression '-->' NonEmptyExpression

Variable ::=
	Word
	| 'nonop' Operation
	| 'nonmac' Macro
	| 'nonsyntax' SyntaxWord

Declaration ::=
	'global'? ('vars' | 'constant') Varslist ';'
	| 'lvars' (',' | Word)+ ';'
	| 'dlocal' (',' | Word)+ ';'

Varslist ::=
	(','* ('procedure' | 'macro' | 'syntax' | Number)? Word)+

Sequence ::=
	(NonEmptyExpression (',' | ';' | '=>' | '==>'))+

Literal ::=
	Number
	| String

Construct ::=
	ListConstruct
	| PatternConstruct
	| VectorConstruct

VectorConstruct ::=
	'{' ListSegment* '}'

ListConstruct ::=
	'[' ListSegment* ']'

PatternConstruct ::=
	'[' PatternSegment ']'

ListSegment ::=
	'%' Sequence '%'
	| Word
	| Literal
	| Construct
	| ('^' | '^^') (Word | '(' Sequence ')')

PatternSegment ::=
	ListSegment
	| ('?' | '??') Word (':' (Integer | Word | ListSegment))?
	| '='
	| '=='

Label ::=
	Word ':' '*'?

Jump ::=
	'goto' Word

Iterative ::=
	'for' NonEmptyExpression 'step' NonEmptyExpression 'till' NonEmptyExpression 'do' Sequence 'endfor'
	| 'while' NonEmptyExpression 'do' Sequence 'endwhile'
	| 'until' NonEmptyExpression 'do' Sequence 'enduntil'
	| 'for' Word (('in' | 'on') | ('from' NonEmptyExpression ('by' NonEmptyExpression)?)? 'to') NonEmptyExpression 'do' Sequence 'endfor'
	| 'for' Word 'from_repeater' NonEmptyExpression 'do' Sequence 'endfor'
	| 'foreach' NonEmptyExpression ('in' NonEmptyExpression)? 'do' Sequence 'endforeach'
	| 'forevery' NonEmptyExpression ('in' NonEmptyExpression)? 'do' Sequence 'endforevery'
	| 'repeat' (NonEmptyExpression 'times' | 'forever')? Sequence 'endrepeat'

Conditional ::=
	'if' NonEmptyExpression 'then' Sequence (('elseif' | 'elseunless') NonEmptyExpression 'then' Sequence)* ('else' NonEmptyExpression)? 'endif'
	| 'unless' NonEmptyExpression 'then' Sequence (('elseif' | 'elseunless') NonEmptyExpression 'then' Sequence)* ('else' NonEmptyExpression)? 'endunless'

BooleanExpression ::=
	NonEmptyExpression (('and' | 'or') NonEmptyExpression)*

OperatorExpression ::=
	NonEmptyExpression Operator NonEmptyExpression

FunctionApplication ::=
	NonEmptyExpression '(' Expression (',' Expression)* ')'
	| '.' NonEmptyExpression
